title: Dawn.Elder.Start
tags: 
colorID: 0
position: 630,-61
---
<<if visited("Dusk.Elder.Start") is true>>
    <<SetSpeaker Rat>>
    MOUSE: Good luck on your journey, Dawn. Keep my words in mind. Remember, take your time.
    <<SetSpeaker Dawn>>    
    DAWN: Yes! Thank you, Elderly!
    <<stop>>
<<endif>>

<<if visited("Dawn.Elder.Elixir") is true>>
    <<SetSpeaker Rat>>
    MOUSE: You can now communicate with Dusk. Press ‘R’ to SWITCH to Dusk.
    <<stop>>
<<endif>>

<<if visited("Dawn.Elder.Gem") is true>>
    <<SetSpeaker Rat>>    
    MOUSE: Press ‘B’ to OPEN your inventory. CLICK on the item using your mouse to drink it.
    <<stop>>
<<endif>>

<<if $give_dawn_tutorial_gem is true>>
    <<SetSpeaker Rat>>
    MOUSE: Pick up the sun gem by moving on top of the gem.
    <<stop>>
<<endif>>


<<SetSpeaker Rat>>
MOUSE ELDERLY: Dawn, you're the Chosen this year. You must journey to the top of the tower.
<<SetSpeaker Dawn happy>>
DAWN: I'm honoured to have this experience. I've dreamt of being a Chosen ever since I've heard stories of how beautiful twilight is.
DAWN: I've read the books you've written about twilight, Elderly. 
DAWN:It feels like a dream...witnessing something so unreal. I can't wait!
<<SetSpeaker Rat happy>>
MOUSE: Haha, as eager as ever, Dawn. However, pace yourself, for haste leads to poor decisions. 
MOUSE: Slow and steady, young Dawn. Remember, this is a moment that you must walk slowly and focus on your surroundings other than yourself.
<<SetSpeaker Dawn>>
DAWN: I see...I guess I was too eager. I shall follow your advice. 
<<SetSpeaker Rat>>
MOUSE: Listen well, you need FOUR sun gems. I have the first 1 for you. Go and pick it up.
<<set $give_dawn_tutorial_gem to true>>
<<stop>>
===
title: Dawn.Elder.Gem
tags: 
colorID: 0
position: 857,-58
---
Mouse: You have your first gem out of four!
Mouse: Don’t forget on this tower, Dawn can only interact with 2 sides and Dusk with the other 2 sides.
Dawn: Dusk? 
Mouse: Yes. There are two Chosens. Did you forget? 
Dawn: ...Maybe...heh..
Mouse: Oh, Dawn...Forgetful as ever, I see. Dusk is the other chosen. She'll be your partner throughout the journey. 
Mouse: You won't be able to see her but you need to work together. How? Well...
Mouse: Drink this. Here is the Elixir of Twilight.
Mouse: Press ‘B’ to open your inventory. Click on the item using your mouse to drink it.
<<set $give_dawn_elixir to true>>
===
title: Dawn.Elder.Elixir
tags: 
colorID: 0
position: 1095,-56
---
Mouse: You can now communicate with Dusk. Press ‘R’ to switch to Dusk.
===
title: Dusk.Elder.Start
tags: 
colorID: 0
position: 627,212
---
<<if visited("Dusk.Elder.Gem") is true>>
    Frog: Good luck on your Journey, Dusk.
    Frog: I hope you learn to leave your past...Open your heart up, Dusk, and rely on others.
    <<stop>>
<<endif>>

<<if visited("Dusk.Elder.Elixir") is true>>
    Elder: You need PRESS 'R' to switch to Dawn to get this gem.
    Elder: Maybe there's a switch on Dawn's side you need to activate.
    <<stop>>
<<endif>>

<<if $give_dusk_elixir is true>>
    Elder: Press 'B' to open your inventory and click on the Elixir with your mouse button to drink it.
    <<stop>>
<<endif>>

Elder Frog: You are the Chosen this year --
Dusk: Yup. I've got it. Just gimme the Gem and Elixir and let’s get this over with. I'm...not really in the mood to really interact with others right now. 
Frog: ... Very well, here's the elixir. Try to cooperate with Dawn, you cannot do everything alone, Dusk. 
Frog:You know that best. I know you do.
Dusk: I'm not here for that right now, Elder. You know that best.
Frog: *Sigh*
Frog: Don't forget you need to press 'B' to open up your inventory then click on the Elixir with your mouse button.

<<set $give_dusk_elixir to true>>
<<set $enable_dusk_tutorial_switch to true>>
<<stop>>
===
title: Dawn.Tutorial.Switch
tags: 
colorID: 0
position: 256,-57
---
<<if visited("Dusk.Elder.Gem") is true>>
    DAWN: That's all the switch does, I think I helped out Dusk! We're going to work SOOOOO well together, I can feel it!!
    <<stop>>
<<endif>>
<<if visited("Dusk.Elder.Start") is true>>
    <<if $turned_on_switch is true>>
        DAWN: I should switch back to Dusk.
        <<stop>>
    <<endif>>
    DAWN: Here's a switch... This should help Dusk if I press it.
    [[ Press Switch |Dawn.Tutorial.Press]]
    [[ Ignore Switch |Dawn.Tutorial.Ignore]]
<<else>>
    DAWN: This appears to be a switch, but it doesn't appear to do anything on MY SIDE.
<<endif>>
===
title: Dusk.Elder.Gem
tags: 
colorID: 0
position: 1095,202
---
Dusk: Finally got the gem...
Frog: Well? Like I said Dusk, you need to work with others sometimes.
Frog: If a path seems unpassable TRY SWITCHING TO DAWN. Maybe you need her to help you. 
Frog: So...will you just do me one last favour? Be kind to her and help her, okay? 
Dusk: ...Alright. 
Frog: Good luck on your Journey, Dusk.
Frog: I hope you learn to leave your past...Open your heart up, Dusk, and rely on others.
<<set $done_tutorial to true>>
===
title: Dusk.Elder.Elixir
tags: 
colorID: 0
position: 860,212
---
Frog: You can now communicate with Dawn.
Frog: and... HERE'S THE GEM! Try to get this by yourself!
<<set $give_dusk_tutorial_gem to true>>
Frog: You need to PRESS 'R' to switch to Dawn to get this gem, It's too high up for you to jump.
Dusk: You're doing this on purpose, aren't you?
Frog: Haha, of course I am! 'Teamwork makes the dream work', as they say.
===
title: Tutorial.Greeting
tags: 
colorID: 0
position: 252,189
---
Dusk: Hello? 
Dawn: WHAAAT! Is that you, Dusk?!
Dusk: (...)  So, I guess your Dawn.
Dawn: Whoa ! Yeah, it's nice to meet you!
Dusk: Yeah, nice to meet you too.
Dawn: I'm so excited to be working with you ! I hope we get along. I'm SO EXCITED TO GO ON THIS JOURNEY, I'VE ONLY HEARD STORIES OF HOW BEAUTIFUL TWILIGHT IS!!!!
Dawn: And I grew up reading all the books in the library about the phenomenon! *continues to ramble*
Dusk: (She talks so much and so fast, I can't keep up.) Same. Uh, so listen this Old Frog wants me to ask you for help.
Dawn: Yeah, of course ! What can I do to help you though? We're on different sides of the tower.
Dusk: Im not sure. The frog mentioned that there should be a SWITCH on your side of the tower though. Activating it should do something to help me.
Dawn: Alright. I'll look for the switch then. Give me a moment!

<<stop>>
===
title: Dawn.Tutorial.Press
tags: 
colorID: 0
position: -58,-381
---
Dusk: Oh! A platform appeared on my side!
Dawn: Perfect! It seems like it all worked out. (I should switch back to Dusk.)
<<set $turned_on_switch to true>>
===
title: Dawn.Tutorial.Ignore
tags: 
colorID: 0
position: 513,-392
---
Dawn: Guess I'll ignore it for now.
===
